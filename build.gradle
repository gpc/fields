buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.grails.org/grails/core" }
        mavenLocal()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
        classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if ((details.requested.group == 'org.codehaus.groovy' || details.requested.group == 'org.apache.groovy') && details.requested.name != 'groovy-bom') {
                details.useTarget(group: 'org.codehaus.groovy', name: details.requested.name, version: "$GroovySystem.version")
                details.because "Use version of Groovy provided by Gradle"
            }
        }
    }
}
plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group 'io.github.gpc'

apply plugin: 'org.grails.grails-plugin'
apply plugin: 'org.grails.grails-gsp'
apply plugin: "org.grails.grails-doc"
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "io.github.gradle-nexus.publish-plugin"

sourceCompatibility = 1.17
targetCompatibility = 1.17

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    mavenLocal()
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-logging'
    compileOnly "org.springframework.boot:spring-boot-starter-actuator"
    compileOnly "org.springframework.boot:spring-boot-autoconfigure"
    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"
    compileOnly "org.grails:grails-dependencies"
    compileOnly "org.grails.plugins:gsp:${grailsVersion}"
    implementation "org.grails:scaffolding-core:$grailsScaffoldingVersion"

    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails:grails-gorm-testing-support"

    testImplementation 'org.javassist:javassist:3.29.0-GA'
    testImplementation "org.apache.groovy:groovy-dateutil"
    testImplementation("org.jodd:jodd-wot:$joddWotVersion") {
        exclude module: 'slf4j-api'
        exclude module: 'asm'
    }

    testRuntimeOnly "net.bytebuddy:byte-buddy:$byteBuddyVersion"
}

tasks.withType(GroovyCompile).configureEach {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'fields'
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Fields'
                description = 'Fields integration for Grails'
                url = 'https://github.com/gpc/fields'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'robfletcher'
                        name = 'Rob Fletcher'
                    }
                    developer {
                        id = 'graemerocher'
                        name = 'Graeme Rocher'
                    } 
                    developer {
                        id = 'sdelamo'
                        name = 'Sergio del Amo'
                    }
                    developer {
                        id = 'sbglasius'
                        name = 'SÃ¸ren Berg Glasius'
                        email = 'soeren+gpc@glasius.dk'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/gpc/fields.git'
                    developerConnection = 'scm:git:https://github.com/gpc/fields.git'
                    url = 'https://github.com/gpc/fields'
                }
            }
        }
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

if(System.getenv('SIGNING_KEY_ID')) {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSPHRASE')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_FILE')
}

afterEvaluate {
    signing {
        if (System.getenv('SIGN_ARMORED_KEY')) {
            String signingKey = System.getenv('SIGN_ARMORED_KEY')
            String signingPassword = System.getenv('SIGN_PASSWORD')
            useInMemoryPgpKeys(signingKey, signingPassword)
        } 
        required {
            isReleaseVersion
        }

        sign publishing.publications.maven
    }
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}

import io.github.gradlenexus.publishplugin.InitializeNexusStagingRepository

tasks.withType(InitializeNexusStagingRepository).configureEach {
    onlyIf { isReleaseVersion }
    shouldRunAfter(tasks.withType(Sign))
}

nexusPublishing {
	repositories {
		sonatype {
			def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
			def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
			def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileIdExternalConfig") ? project.sonatypeOssStagingProfileIdExternalConfig : ''

			nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
			snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
			username = ossUser
			password = ossPass
			stagingProfileId = ossStagingProfileId
		}
	}
}

asciidoctor {
    resources {
        from('src/docs/images')
        into "./images"
    }
    sources {
        include 'index.adoc'
    }
    attributes 'experimental': 'true',
            'compat-mode': 'true',
            'toc': 'left',
            'icons': 'font',
            'version': project.version,
            'sourcedir': 'src/main/groovy'
    baseDirFollowsSourceDir()
    outputDir = "${buildDir}/asciidoc"
}

tasks.register('apiDocs', Copy) {
    from groovydoc.outputs.files
    into file("${buildDir}/asciidoc/api")
}

asciidoctor.dependsOn(apiDocs)

tasks.register('snapshotVersion') {
    doLast {
        if (isReleaseVersion) {
            ant.propertyfile(file: "gradle.properties") {
                entry(key: "version", value: "${project.version}-SNAPSHOT")
            }
        }
    }
}
