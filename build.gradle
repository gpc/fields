buildscript {
    ext {
      grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:2.1.1'
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.2.RELEASE"
    id "com.jfrog.bintray" version "1.2"
}

version "2.1.1-SNAPSHOT"
group "org.grails.plugins"

bintray {
    user = System.getenv('BINTRAY_USER') ?: project.hasProperty('bintrayUser') ? project.bintrayUser : ''
    key = System.getenv('BINTRAY_KEY') ?: project.hasProperty('bintrayKey') ? project.bintrayKey : ''
    publications = ['maven']
    publish = true
    pkg {
        repo = 'plugins'
        userOrg = 'grails'
        name = "org.grails.plugins:$project.name"
        desc = "Grails $project.name plugin"
        issueTrackerUrl = 'https://github.com/grails-fields-plugin/grails-fields/issues'
        vcsUrl = 'https://github.com/grails-fields-plugin/grails-fields'

        version {
            attributes = ['grails-plugin': "org.grails.plugins:$project.name"]
            name = project.version
        }
    }
}

apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "spring-boot"
apply plugin: "asset-pipeline"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"

apply plugin: "org.grails.grails-doc"
// Used for publishing to central repository, remove if not needed
apply from:'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/grailsCentralPublishing.gradle'
apply from:'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/bintrayPublishing.gradle'

ext {
  grailsVersion = project.grailsVersion
  gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
  provided 'org.springframework.boot:spring-boot-starter-logging'
  provided "org.springframework.boot:spring-boot-starter-actuator"
  provided "org.springframework.boot:spring-boot-autoconfigure"
  provided "org.springframework.boot:spring-boot-starter-tomcat"

  provided "org.grails:grails-web-boot"
  provided "org.grails:grails-dependencies"
  provided 'javax.servlet:javax.servlet-api:3.1.0'

  testCompile "org.grails:grails-plugin-testing"

  console "org.grails:grails-console"

  testCompile 'javassist:javassist:3.12.0.GA'
  testCompile('org.jodd:jodd-wot:3.3.4') {
        exclude module:'slf4j-api'
        exclude module:'asm'
  }
}

eclipse {
    classpath {
        plusConfigurations += [configurations.provided]
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}
